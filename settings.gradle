pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://maven.lukebemish.dev/releases/"
        }
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.17'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.5.0'
}

gradleEnterprise {
    if (System.getenv('CI') != null) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

rootProject.name = 'opensesame'

def subprojects = [
        'core',
        'compile',
        'groovy',
        'javac',
        'plugin-core',
        'plugin-loom',
        'fabric',
        'mixin'
]

include subprojects

subprojects.each {
    def p = project(":$it")
    p.projectDir = file(it.replace('-', '/'))
    p.name = "opensesame-$it"
}

include 'testtargets'

buildCache {
    if (providers.gradleProperty('buildCacheUrl').orNull) {
        remote(HttpBuildCache) {
            url = providers.gradleProperty('buildCacheUrl').orNull
            credentials {
                username = providers.gradleProperty('buildCacheUser').orNull
                password = providers.gradleProperty('buildCachePassword').orNull
            }
            if (providers.gradleProperty('buildCachePush').orNull) {
                push = true
            } else {
                push = false
            }
        }
    } else if (System.getenv('BUILD_CACHE_URL')) {
        remote(HttpBuildCache) {
            url = System.getenv('BUILD_CACHE_URL')
            credentials {
                username = System.getenv('BUILD_CACHE_USER')
                password = System.getenv('BUILD_CACHE_PASSWORD')
            }
            if (System.getenv('BUILD_CACHE_PUSH') || System.getenv('CI')) {
                push = true
            } else {
                push = false
            }
        }
    }
}