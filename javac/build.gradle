plugins {
    id 'opensesame.conventions'
}

java.withSourcesJar()
java.withJavadocJar()

dependencies {
    implementation project(':opensesame-compile')
    api project(':opensesame-annotations')
    implementation libs.asm
}

tasks.named('compileTestJava', JavaCompile).configure {
    options.compilerArgs.add '-Xplugin:OpenSesame'
    dependsOn(tasks.compileJava)
    inputs.files(sourceSets.main.output.classesDirs)
    inputs.files(sourceSets.main.runtimeClasspath)
}

def testCompileOn(int v) {
    var singleTest = tasks.register('testCompileOn'+v, JavaExec) {
        javaLauncher.set javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(v)
        }

        dependsOn configurations.testRuntimeClasspath
        dependsOn sourceSets.test.java.sourceDirectories

        var dirProvider = layout.buildDirectory.dir('testClasses')

        outputs.dir(dirProvider)
        inputs.files(sourceSets.test.java.sourceDirectories)

        group = "Verification"
        description = "Compile test sources with java"
        classpath = sourceSets.main.runtimeClasspath
        mainClass.set 'com.sun.tools.javac.Main'
        argumentProviders.add(
                new CommandLineArgumentProvider() {
                    @Override
                    List<String> asArguments() {
                        return [
                                '-cp', configurations.testCompileClasspath.asPath,
                                '-d', dirProvider.get().asFile.path,
                                '-Xplugin:OpenSesame'
                        ] +
                                sourceSets.test.java.srcDirs.collectMany { fileTree(it.path).files.collect { it.path } }
                    }
                }
        )

        jacoco.applyTo(it)
        extensions.configure(JacocoTaskExtension) {
            it.excludes += 'dev/lukebemish/opensesame/test/target/*'
        }
        outputs.upToDateWhen { false }

        finalizedBy tasks.named('testCompileOn'+v+'CodeCoverateReport')
    }

    var report = tasks.register('testCompileOn'+v+'CodeCoverateReport', JacocoReport) {
        dependsOn singleTest.get()
        executionData singleTest.get()
        sourceSets sourceSets.main
    }

    artifacts {
        add('coverageDataElementsForTest', report.get().executionData.singleFile) {
            builtBy report.get()
        }
    }
}

testCompileOn(17)
testCompileOn(21)

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "OpenSesame - Javac Compiler Plugin"
                packaging = 'jar'
                description = 'javac compiler plugin for OpenSesame, a tool for typesafe access normally inacessible members'
                url = 'https://github.com/lukebemish/OpenSesame'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = '3-Clause BSD License'
                        url = 'https://opensource.org/license/bsd-3-clause/'
                    }
                }
                developers {
                    developer {
                        id = 'lukebemish'
                        name = 'Luke Bemish'
                        email = 'lukebemish@lukebemish.dev'
                        url = 'https://github.com/lukebemish/'
                    }
                }
                scm {
                    connection='scm:git:git://github.com/lukebemish/OpenSesame.git'
                    developerConnection='scm:git:ssh://github.com/lukebemish/OpenSesame.git'
                    url='https://github.com/lukebemish/OpenSesame'
                }
            }
        }
    }
}
