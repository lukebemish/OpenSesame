java.withSourcesJar()
java.withJavadocJar()

dependencies {
    implementation project(':opensesame-compile')

    testImplementation project(':opensesame-runtime').sourceSets.test.output
}

tasks.named('compileTestJava', JavaCompile).configure {
    it.options.compilerArgs.add '-Xplugin:OpenSesame'
    it.dependsOn(tasks.compileJava)
    it.inputs.files(sourceSets.main.output.classesDirs)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "OpenSesame - Javac Compiler Plugin"
                packaging = 'jar'
                description = 'javac compiler plugin for OpenSesame, a tool for typesafe access normally inacessible members'
                url = 'https://github.com/lukebemish/OpenSesame'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = '3-Clause BSD License'
                        url = 'https://opensource.org/license/bsd-3-clause/'
                    }
                }
                developers {
                    developer {
                        id = 'lukebemish'
                        name = 'Luke Bemish'
                        email = 'lukebemish@lukebemish.dev'
                        url = 'https://github.com/lukebemish/'
                    }
                }
                scm {
                    connection='scm:git:git://github.com/lukebemish/OpenSesame.git'
                    developerConnection='scm:git:ssh://github.com/lukebemish/OpenSesame.git'
                    url='https://github.com/lukebemish/OpenSesame'
                }
            }
        }
    }
}
