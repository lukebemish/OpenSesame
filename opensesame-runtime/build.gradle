java.withSourcesJar()
java.withJavadocJar()

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.1'
}

publishing {
    publications {
        mavenRuntime(MavenPublication) {
            from components.java

            pom {
                name = "OpenSesame - Runtime"
                packaging = 'jar'
                description = 'Runtime metafactory used for OpenSesame, a tool for fase typesafe access of private members with groovy'
                url = 'https://github.com/lukebemish/OpenSesame'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = '3-Clause BSD License'
                        url = 'https://opensource.org/license/bsd-3-clause/'
                    }
                }
                developers {
                    developer {
                        id = 'lukebemish'
                        name = 'Luke Bemish'
                        email = 'lukebemish@lukebemish.dev'
                        url = 'https://github.com/lukebemish/'
                    }
                }
                scm {
                    connection='scm:git:git://github.com/lukebemish/OpenSesame.git'
                    developerConnection='scm:git:ssh://github.com/lukebemish/OpenSesame.git'
                    url='https://github.com/lukebemish/OpenSesame'
                }
            }
        }
    }
}

if (System.getenv('IS_RELEASE') || System.getenv('SNAPSHOT_MAVEN_URL')) {
    signing {
        final signingKey = System.getenv('SIGNING_KEY') ?: ''
        final signingPassword = System.getenv('SIGNING_PASSWORD') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenRuntime
    }
}
