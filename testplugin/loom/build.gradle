plugins {
    id 'java'
    id 'fabric-loom'
    id 'dev.lukebemish.opensesame.loom'
}

group='dev.lukebemish.opensesame'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

configurations {
    testSource {
        canBeResolved = true
    }
    testClasses {
        canBeResolved = false
        canBeConsumed = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft 'com.mojang:minecraft:1.20.4'
    mappings loom.officialMojangMappings()

    testSource('dev.lukebemish.opensesame:testtargets:testsources') {
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, 'java-source'))
        }
    }
    implementation 'dev.lukebemish.opensesame:testtargets'
    implementation 'dev.lukebemish.opensesame:opensesame-core'
    implementation libs.asm.core
    implementation libs.junit.api
}

opensesame.loom.apply()

tasks.named('compileJava', JavaCompile).configure {
    dependsOn(configurations.testSource)
    source(configurations.testSource)
}

def unpackedDir = layout.buildDirectory.dir('unpacked')

tasks.register('unpackRemappedJar', Copy).configure {
    dependsOn tasks.remapJar
    from zipTree(tasks.remapJar.archiveFile)
    into unpackedDir
}

artifacts {
    add('testClasses', unpackedDir) {
        builtBy tasks.unpackRemappedJar
    }
}