plugins {
    id 'java'
    id 'fabric-loom'
    id 'dev.lukebemish.opensesame.loom'
}

group='dev.lukebemish.opensesame'

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))

loom {
    runs.removeAll()
}

opensesame.apply(sourceSets.main)

configurations {
    testSource {
        canBeResolved = true
    }
    testClasses {
        canBeResolved = false
        canBeConsumed = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft 'com.mojang:minecraft:1.20.4'
    mappings loom.officialMojangMappings()

    testSource('dev.lukebemish.opensesame:testtargets:testsources') {
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, 'java-source'))
        }
    }
    implementation 'dev.lukebemish.opensesame:testtargets'
    implementation 'dev.lukebemish.opensesame:opensesame-core'
    implementation libs.asm.core
    implementation libs.junit.api
    modImplementation libs.fabric.loader

    testRuntimeOnly libs.junit.engine
}

tasks.named('compileJava', JavaCompile).configure {
    dependsOn(configurations.testSource)
    source(configurations.testSource)
}

artifacts {
    testClasses tasks.remapJar
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events = ['passed', 'failed', 'skipped']
    }

    testClassesDirs += files(
            sourceSets.main.output
    )

    classpath += files(
            sourceSets.main.output
    )

    outputs.upToDateWhen {false}
}