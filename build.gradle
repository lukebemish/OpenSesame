plugins {
    alias libs.plugins.managedversioning
    alias libs.plugins.nexuspublish
    id 'jacoco-report-aggregation'
    id 'test-report-aggregation'
    id 'jvm-test-suite'
}

managedVersioning {
    versionFile.set rootProject.file('version.properties')
    versionPRs()
    versionSnapshots()
}

managedVersioning.apply()

repositories {
    mavenCentral()
    maven {
        name = 'FabricMC'
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    subprojects.each { p ->
        if (p.name.startsWith('opensesame-')) {
            jacocoAggregation p
            testReportAggregation p
        }
    }
}

println "Building: ${rootProject.version}"

if (!System.getenv('PR_NUMBER') && !System.getenv('SNAPSHOT_MAVEN_URL')) {
    nexusPublishing {
        repositories {
            sonatype {
                username.set(System.getenv('SONATYPE_USER') ?: '')
                password.set(System.getenv('SONATYPE_PASSWORD') ?: '')
                nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            }
        }

    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
            reportTask.configure {
                reports.xml.required = true
            }
        }
        testAggregateTestReport(AggregateTestReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.named('check').configure {
    dependsOn tasks.testCodeCoverageReport
    dependsOn tasks.testAggregateTestReport
}