plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.1.0'
}

java.toolchain.languageVersion.set JavaLanguageVersion.of(17)

group = rootProject.group
version = rootProject.version

repositories {
    mavenCentral()
    maven {
        name = 'FabricMC'
        url = 'https://maven.fabricmc.net/'
    }
}

java.withSourcesJar()
java.withJavadocJar()

dependencies {
    compileOnly libs.jetbrains.annotations
    annotationProcessor(libs.autoservice)
    compileOnly(libs.autoservice)

    compileOnly libs.fabric.loom
    implementation libs.asm

    implementation project(':opensesame-compile')
}

gradlePlugin {
    website = 'https://github.com/lukebemish/OpenSesame'
    vcsUrl = 'https://github.com/lukebemish/OpenSesame.git'

    plugins {
        openSesame {
            id = 'dev.lukebemish.opensesame'
            implementationClass = 'dev.lukebemish.opensesame.plugin.OpenSesamePlugin'
            displayName = 'OpenSesame - Gradle Plugin'
            description = 'Gradle plugin for OpenSesame, a tool for typesafe access to normally inacessible members'
            tags.addAll(['java', 'mapping'])
        }
    }
}

processResources {
    from rootProject.file('LICENSE')
}

if (System.getenv('PR_NUMBER')) {
    publishing {
        repositories {
            maven {
                name = 'LocalMaven'
                url = rootProject.layout.buildDirectory.dir('repo')
            }
        }
    }
} else if (System.getenv('SNAPSHOT_MAVEN_URL')) {
    publishing {
        repositories {
            maven {
                name = 'PersonalMaven'
                url = uri(System.getenv('SNAPSHOT_MAVEN_URL'))
                credentials {
                    username = System.getenv('MAVEN_USER')
                    password = System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }
}